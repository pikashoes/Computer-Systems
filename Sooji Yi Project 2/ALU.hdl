// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:

    // Build zx and zy first.
    Mux16(a = x, b = false, sel = zx, out = outX);
    Mux16(a = y, b = false, sel = zy, out = outY);

    // Prep for nx and ny
    Not16(in = outX, out = notOutX);
    Not16(in = outY, out = notOutY);

    // Build nx and ny
    Mux16(a = outX, b = notOutX, sel = nx, out = outX2);
    Mux16(a = outY, b = notOutY, sel = ny, out = outY2);

    // Prep for f
    And16(a = outX2, b = outY2, out = outXandY);
    Add16(a = outX2, b = outY2, out = outXaddY);
    
    // Build f
    Mux16(a = outXandY, b = outXaddY, sel = f, out = outTemp);

    // Build ng
    Not16(in = outTemp, out = notOutTemp);
    Mux16(a = outTemp, b = notOutTemp, sel = no, out[15] = ng, out[0..7] = outFirst, out[8..15] = outSecond, out = out);

    // Build zr
    Or8Way(in = outFirst, out = orFirst);
    Or8Way(in = outSecond, out = orSecond);
    Or(a = orFirst, b = orSecond, out = outZero);

    Not(in = outZero, out = zr);

}